#!/usr/bin/env nu

const usrSharePath = "/usr/share/bluebuild/default-flatpaks"
const configPath = $"($usrSharePath)/configuration.yaml"

def main [] {
    let configFile = open $configPath

    let systemRemotes = (flatpak remotes --system --columns name | split row "\n")
    if ($systemRemotes | any {|remote| $remote == "fedora" or $remote == "fedora-testing"}) {
        /usr/bin/gnome-software --quit
        /usr/lib/fedora-third-party/fedora-third-party-opt-out
        /usr/bin/fedora-third-party disable

        if ($systemRemotes | any {|remote| $remote == "fedora"}) {
            flatpak remote-delete --system fedora --force
        }
        if ($systemRemotes | any {|remote| $remote == "fedora-testing"}) {
            flatpak remote-delete --system fedora-testing --force
        }

        # TODO remove Fedora Flatpaks
    }

    $configFile.configurations | where scope == system | each { |config|
        flatpak remote-add --system --if-not-exists $config.repo.name $config.repo.url --title $config.repo.title

        if ($config.notify) {
            (notify-send-as-user
                "--app-name" "Automatic Flatpak Installation Service"
                $"Starting automated installation of ($config.install | length) system Flatpak\(s) from ($config.repo.title)..."
            )
        }

        flatpak install --system $config.repo.name ...$config.install

        if ($config.notify) {
            (notify-send-as-user
                "--app-name" "Automatic Flatpak Installation Service"
                $"Finished automated installation of ($config.install | length) system Flatpak\(s) from ($config.repo.title)!"
                ($config.install | str join ', ')
            )
        }
    }
}

def notify-send-as-user [...args] {
    let user_name = (loginctl list-sessions --json=short | from json | get user | get 0)
    let uid = (loginctl list-sessions --json=short | from json | get uid | get 0)
    let xdg_runtime_path = $"/run/user/($uid)"
    sudo -u $user_name DBUS_SESSION_BUS_ADDRESS=unix:path=($xdg_runtime_path)/bus notify-send ...$args
}
